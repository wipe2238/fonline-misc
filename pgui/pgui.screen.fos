//
// PGUI
// Primitive GUI for FOnline based games
// Wipe/Rotators
//

#ifndef __PGUI_SCREEN__
#define __PGUI_SCREEN__

#include "pgui.fos"

class PGUIScreen : PGUIElement
{
    uint ID, Layer;
    bool canMove;
    bool ignoreBorders;

    bool drawFrame, drawBackground;
    int colorFrame, colorBackground;

    PGUIScreen( uint layer )
    {
        super();
        this.PGUIScreenInit( 0, 0, layer );
    }

    PGUIScreen( uint width, uint height, uint layer )
    {
        super();
        this.PGUIScreenInit( width, height, layer );
    }

    private void PGUIScreenInit( uint width, uint height, uint layer ) final
    {
        this.ID = 0;
        this.Layer = layer;
        this.ignoreBorders = false;
        this.Width = width;
        this.Height = height;
        this.canMove = this.drawFrame = this.drawBackground = true;
        this.colorFrame = PGUI_RGBi(0,255,0);
        this.colorBackground = PGUI_RGBAi(0,255,0,100);
    }

    //
    // PGUIElement overrides
    //

    void MouseMove( int fromX, int fromY, int toX, int toY ) override
    {
        if( this.mousePressed && this.mouseClick == PGUI_CLICK_LEFT )
        {
            int lastX = this.mouseX;
            int lastY = this.mouseY;
            int offX = toX - fromX;
            int offY = toY - fromY;

            this.Left = this.Left + offX;
            this.Top  = this.Top  + offY;

            if( !this.ignoreBorders )
            {
                if( this.Left + offX < 0 )
                    this.Left = 0;
                if( this.Top + offY < 0 )
                    this.Top = 0;
                if( this.Right > __ScreenWidth )
                    this.Left = __ScreenWidth - this.Width;
                if( this.Bottom > __ScreenHeight )
                    this.Top = __ScreenHeight - this.Height;
            }

            PGUIElement::MouseMove( fromX, fromY, toX, toY );
        }
    }

    void Update() override
    {
    }

    void Draw() override
    {
        if( this.drawBackground )
            this.DrawBackground( this.colorBackground );
        if( this.drawFrame )
            this.DrawFrame( this.colorFrame );

        PGUIElement::Draw();
    }
};

#endif // __PGUI_SCREEN__ //

